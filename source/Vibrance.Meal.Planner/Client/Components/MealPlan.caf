import &StandardImport

class MealPlan extends FluxComponent
  @subscriptions :mealPlan

  render: ->
    {gridSize} = ViewLayout
    Element
      childrenLayout: :column

      &Banner
        back:     @props.home
        title:    "" Meal Plan

      TextElement
        TextStyles.tinyText
        :childrenSize
        padding:  h: gridSize, top: gridSize / 2
        text:     "" meal plan name

      Element
        :parentWidthChildrenHeight
        :row
        draw:
          padding: 5
          rectangle: radius: 5
          outline: TextPalette.black.tertiary

        TextInputElement
          TextStyles.mediumText
          size:         ww:1 h: 50
          padding:      h: 10
          placeholder:  "" menu plan name
          maxLength:    100
          value:        @mealPlan.name
          on: valueChanged: (event) ->
            @models.mealPlan.update @mealPlan.id, name: event.props.value
        TextElement
          Icons.pencil
          align: .5
          size: ViewLayout.buttonSize

      array recipePlanId, i from @mealPlan.recipePlanIds
        &RecipePlan {}
          key: recipePlanId
          recipePlanId

      Element size: h: 10

      &Button
        text: "add recipe to this meal plan"
        action: -> @models.navState.pickRecipeProps = pick:
          (recipeId) ->
            @models.mealPlan.addRecipePlan @mealPlan.id, {recipeId, mealPlanId: @mealPlan.id}

      if  @mealPlan?.recipePlanIds?.length > 0

        &Button
          text: "go to shopping list"
          action: ->
            @models.navState.shoppingList = {}
              @mealPlan
              shoppingList: @models.mealPlan.getShoppingList @mealPlan.id
